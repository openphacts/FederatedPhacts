#+ summary: Compound information query for a given wikidata compound
#+ endpoint: https://query.wikidata.org/sparql
#+ method: GET
#+ pagination: 50
#+ tags:
#+   - wikidata
#+ defaults:
#+   - molecule: http://www.wikidata.org/entity/Q18216

PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd:  <http://www.wikidata.org/entity/>

SELECT ?moleculeLabel ?formula ?smiles ?inchikey ?inchi ?logp ?molweight
WHERE {
    BIND(?_molecule_iri AS ?molecule)
    ?molecule wdt:P274 ?formula .
    ?molecule rdfs:label ?moleculeLabel
    FILTER (langMatches( lang(?moleculeLabel), "EN" ) )
    OPTIONAL {?molecule wdt:P233 ?smiles .}
    OPTIONAL {?molecule wdt:P235 ?inchikey ; wdt:P234 ?inchi .}
    OPTIONAL {?molecule wdt:P2993 ?logp .}
    OPTIONAL {?molecule wdt:P2067 ?molweight .}
}
